#! /usr/bin/env stap
#

probe begin {
	printf("begin stopwatch\r\n");
	start_stopwatch("wall")
	start_stopwatch("user")
	stop_stopwatch("system")
}

probe syscall.* {
	if (pid() != target()) next
	stop_stopwatch("user")
	start_stopwatch("system")
}

probe syscall.*.return {
	if (pid() != target()) next
	start_stopwatch("user")
	stop_stopwatch("system")
}

probe end {
	stop_stopwatch("wall")
	stop_stopwatch("user")
	stop_stopwatch("system")

	w = read_stopwatch_us("wall")
	u = read_stopwatch_us("user")
	s = read_stopwatch_us("system")

	printf("wall clock %12dus(100%%)\n", w);
	printf("wall clock in user space %12dus(%d%%)\n", u, u * 100 / w);
	printf("wall clock in kernel space %12dus(%d%%)\n", s, s * 100 / w);
	
	delete_stopwatch("wall")
	delete_stopwatch("user")
	delete_stopwatch("system")
}

